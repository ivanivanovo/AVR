\ ====== прерывания не должны изменять эти регистры ===========================
\ ====== или использовать их как константы ====================================
#def iByte  r0     \ счётчик байт ============================================
#def rPol   r1     \ полином =================================================
\ =============================================================================
\ подсчёт контрольной суммы CRC8 (x^8+x^5+x^4+1), сдвиг влево
code CRC8 ( y=adr r=u -- y=adr+u r=CRC8 ) 
\ вход:  адрес строки (Y) и ее счётчик (r)
\ прим: CRC8 работает до 15 байт данных
\       обнаружение одинарных, двойных, тройных и всех нечетных ошибок 
    push iByte push rPol push rH
    ldi rH,Polynomial mov rPol,rH
        mov iByte,r \ iByte=число байт в строке
        ldi r,0xAC \ начальное значение (CRC8 от FF при стартовом 0)
        for \ цикл по байтам
            ld rH,y+  eor r,rH \ получить новый байт
            ldi rH,8
            for \ цикл по битам
                lsl r if_c eor r,rPol then
            next rH
        next iByte
    pop rH pop rPol pop iByte 
    ret c;


